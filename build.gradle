buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = "${MOD_VERSION_MAJOR}.${MOD_VERSION_MINOR}.${MOD_VERSION_REVIS}"
group = "${MOD_CLASS}.${MOD_ID}"
archivesBaseName = "${MOD_ID}-${MINECRAFT_VERSION}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: "${MAPPINGS_CHANNEL}", version: "${MAPPINGS_VERSION}"

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${MOD_ID}" {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${MOD_ID}" {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'examplemod', '--all', '--output', file('src/generated/resources/')

            mods {
                "${MOD_ID}" {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${FORGE_VERSION}"
    shade "net.dv8tion:JDA:${JDA_VERSION}"
    shade "com.vdurmont:emoji-java:${EMOJI_JAVA_VERSION}"
    shade "io.github.java-diff-utils:java-diff-utils:${JAVA_DIFF_UTILS_VERSION}"
}

repositories {
    jcenter()
    mavenCentral()
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'club.minnced', 'mdcshadow.club.minnced'
    relocate 'com.fasterxml', 'mdcshadow.fasterxml'
    relocate 'com.github.difflib', 'mdcshadow.com.github.difflib'
    relocate 'com.iwebpp', 'mdcshadow.com.iwebpp'
    relocate 'com.neovisionaries', 'mdcshadow.com.neovisionaries'
    relocate 'com.sun', 'mdcshadow.com.sun'
    relocate 'com.vdurmont', 'mdcshadow.com.vdurmont'
    relocate 'gnu', 'mdcshadow.gnu'
    relocate 'okhttp3', 'mdcshadow.okhttp'
    relocate 'okio', 'mdcshadow.okio'
    relocate 'org.apache.commons.collections4', 'mdcshadow.apache-collections4'
    relocate 'org.intellij', 'mdcshadow.intellij'
    relocate 'org.jetbrains', 'mdcshadow.jetbrains'
    relocate 'org.json', 'mdcshadow.org.json'
    relocate 'org.slf4j', 'mdcshadow.slf4j'
    relocate 'tomp2p', 'mdcshadow.tomp2p'
}

artifacts {
    archives tasks.shadowJar
}

reobf {
    shadowJar {}
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${MOD_ID}",
                "Specification-Vendor"    : "${MOD_GROUP}",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${MOD_GROUP}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
